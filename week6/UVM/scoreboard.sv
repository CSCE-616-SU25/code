/*
Scoreboard usage: < ----
Data comparison: Compare the data produced by the DUT with expected results generated by the testbench
The scoreboard is a  DATASTRUCTURE: used for data storage (Stores data or models of the expected behavior)
Error detection: when a mismatch happens between the actual result and the expected result the scoreboard raises errors of assertions*
Communication: Connect to other UVM components
*/


class mem_scoreboard extends uvm_scoreboard;
  
  //---------------------------------------
  // declaring pkt_qu to store the pkt's received from monitor
  //---------------------------------------
  mem_seq_item pkt_qu[$]; // Stores (items) memory transactions
  
  //---------------------------------------
  // sc_mem 
  //---------------------------------------
  // Local memory model created within the scoreboard
  bit [7:0] sc_mem [4]; // This  is an array of bytes (Memory models) - Multiple 8 bit memory locations

  //---------------------------------------
  //port to receive packets from monitor
  //---------------------------------------
  uvm_analysis_imp#(mem_seq_item, mem_scoreboard) item_collected_export; // UVM analysis export
  `uvm_component_utils(mem_scoreboard)

  //---------------------------------------
  // new - constructor
  //---------------------------------------
  function new (string name, uvm_component parent);
    super.new(name, parent);
  endfunction : new
  //---------------------------------------
  // build_phase - create port and initialize local memory
  //---------------------------------------
  function void build_phase(uvm_phase phase);
    super.build_phase(phase);
      item_collected_export = new("item_collected_export", this); // Set up the analysis export to receive data
      foreach(sc_mem[i]) sc_mem[i] = 8'hFF; // Initialize all bytes to 0xFF
  endfunction: build_phase
  
  //---------------------------------------
  // write task - recives the pkt from monitor and pushes into queue
  //---------------------------------------
  virtual function void write(mem_seq_item pkt);
    //pkt.print();
    pkt_qu.push_back(pkt); // Sotre incoming memory transactions in a queue
  endfunction : write

  //---------------------------------------
  // run_phase - compare's the read data with the expected data(stored in local memory)
  // local memory will be updated on the write operation.
  //---------------------------------------
  virtual task run_phase(uvm_phase phase);
    mem_seq_item mem_pkt;
    // Continuously check the memory for incoming transactions
    forever begin
      wait(pkt_qu.size() > 0); // Wait until their is a transaction in the queues
      mem_pkt = pkt_qu.pop_front(); // Dequeue
      
      if(mem_pkt.wr_en) begin // If the transaction is a write
        sc_mem[mem_pkt.addr] = mem_pkt.wdata; // Update the local memory
        `uvm_info(get_type_name(),$sformatf("------ :: WRITE DATA       :: ------"),UVM_LOW)
        `uvm_info(get_type_name(),$sformatf("Addr: %0h",mem_pkt.addr),UVM_LOW)
        `uvm_info(get_type_name(),$sformatf("Data: %0h",mem_pkt.wdata),UVM_LOW)
        `uvm_info(get_type_name(),"------------------------------------",UVM_LOW)        
      end
      else if(mem_pkt.rd_en) begin // If the transaction is a read operation
        if(sc_mem[mem_pkt.addr] == mem_pkt.rdata) begin // !!! Compare the read data in the transaction with the expected data in the local memory model
          `uvm_info(get_type_name(),$sformatf("------ :: READ DATA Match :: ------"),UVM_LOW)
          `uvm_info(get_type_name(),$sformatf("Addr: %0h",mem_pkt.addr),UVM_LOW)
          `uvm_info(get_type_name(),$sformatf("Expected Data: %0h Actual Data: %0h",sc_mem[mem_pkt.addr],mem_pkt.rdata),UVM_LOW)
          `uvm_info(get_type_name(),"------------------------------------",UVM_LOW)
        end
        else begin
          `uvm_error(get_type_name(),"------ :: READ DATA MisMatch :: ------")
          `uvm_info(get_type_name(),$sformatf("Addr: %0h",mem_pkt.addr),UVM_LOW)
          `uvm_info(get_type_name(),$sformatf("Expected Data: %0h Actual Data: %0h",sc_mem[mem_pkt.addr],mem_pkt.rdata),UVM_LOW)
          `uvm_info(get_type_name(),"------------------------------------",UVM_LOW)
        end
      end
    end
  endtask : run_phase
endclass : mem_scoreboard